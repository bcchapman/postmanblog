buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-milestone" }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'querydsl-example'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-milestone" }
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
}

configurations {
    querydslapt
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa:1.2.3.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-data-rest:1.2.3.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-security:1.2.3.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-web:1.2.3.RELEASE")
    compile("com.h2database:h2")
    testCompile("junit:junit")
    
    // for the anontation processor
    querydslapt group: 'com.mysema.querydsl', name: 'querydsl-jpa', version: '3.6.3', classifier: 'apt-one-jar', transitive: false

    // for compiling
    compile("com.mysema.querydsl:querydsl-jpa:3.6.3")
    compile("com.mysema.querydsl:querydsl-apt:3.6.3")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

//Querydsl 
def generatedSrcDir = 'src/main/generated'
task createGeneratedSrcDir << {
    file(generatedSrcDir).mkdirs()
}
compileJava.dependsOn createGeneratedSrcDir

compileJava {
  options.compilerArgs << '-processor' << 'com.mysema.query.apt.jpa.JPAAnnotationProcessor' << '-s' << file(generatedSrcDir).absolutePath
}

clean {
    delete generatedSrcDir
}